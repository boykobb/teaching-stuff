#!/usr/bin/logo
; ┌────────────────────────────────────────────────────────────────────────────────────────────
; │ Удвояване в обобщен смисъл: по зададени функция f(a,b) и стойност x намираме f(x,x)
; └────────────────────────────────────────────────────────────────────────────────────────────
to double :f :x                 ; функция „f-удвоител“
  output (invoke :f :x :x)
end

to recip.sum :a :b
  output :a * :b / (:a + :b)
end

; Примери

setfont "|Ubuntu Mono| settextsize 16

print double "sum 7             ; 14
print double "product 7         ; 49
print double "list 4.17         ; 4.17 4.17
print double "list [3 5 8]      ; [3 5 8] [3 5 8]
print double "sentence [3 5 8]  ; 3 5 8 3 5 8
print double "word "ABC         ; ABCABC
print double "recip.sum 7       ; 3.5
print double [[a b] :a * :b / (:a + :b)] 7
make "f [[a b] :a * :b / (:a + :b)]     ; f е име на променлива (не на процедура), но стойността на променливата е процедура, и то от краткия (inline) вид.
print double :f 7
define "g [[a b] [output :a * :b / (:a + :b)]]
print double "g 7               ; g е име на процедура. При повикване се цитира пряко, както имената на процедурите, определени чрез to.
print g 3 5                     ;

ignore readlist bye
